apply plugin: "com.android.library"
apply plugin: "maven-publish"

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 24
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 28
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    namespace "top.symple.symplegraphdisplay"
}

repositories {
    mavenCentral()
}

dependencies {
    api "org.firstinspires.ftc:RobotCore:9.2.0"
    api "org.firstinspires.ftc:Hardware:9.2.0"
    api "org.firstinspires.ftc:RobotServer:9.2.0"
    api "org.firstinspires.ftc:FtcCommon:9.2.0"

    implementation 'androidx.appcompat:appcompat:1.2.0'
}

task copyDashAssets(type: Copy) {
    from "${project.projectDir}/graph"
    into "${android.sourceSets.getByName("main").assets.srcDirs[0]}/graph"
}

task cleanDashAssets(type: Delete) {
    delete "${android.sourceSets.getByName("main").assets.srcDirs[0]}/graph"
}

tasks.register("printVersionName") {
    println(findProperty("version"))
}

copyDashAssets.dependsOn cleanDashAssets

android.libraryVariants.all { variant ->
    variant.preBuildProvider.get().dependsOn copyDashAssets
}

publishing {
    publications {
        bar(MavenPublication) {
            groupId "top.symple"
            artifactId "symplegraphdisplay"
            version findProperty("version")

            artifact("$buildDir/outputs/aar/symplegraphdisplay-release.aar") {
                builtBy assemble
            }
        }
    }

    repositories {
        maven {
            name "GithubPackages"
            url = uri("https://maven.pkg.github.com/SympleOrg/SympleGraphDisplay")
            credentials {
                username = System.getenv("GITHUB_USER")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}